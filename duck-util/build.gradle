apply plugin: 'java'

sourceCompatibility = 1.6
version = duckVersion

dependencies {
    compile "org.projectlombok:lombok:$lombokVersion"
    compile "org.aspectj:aspectjweaver:$aspectjVersion"

    // Absolutely NO other compile or runtime dependencies are allowed here!

    // All transitive compile and runtime dependencies will end up in the sensored app's system
    // class path. (Lombok will be excluded when assembling the duck-sensor shadow jar).

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task generateTestDuckProperties {
    group "Build"
    description "Generates a duck.properties for test"

    def configFile = file("$projectDir/build/tmp/duck/duck.properties")

    inputs.file file("$projectDir/build.gradle")
    outputs.file configFile

    doLast {
        configFile.parentFile.mkdirs()
        configFile.text = """# Run Duck against Crisp Sample App
appName = Crisp Sample App
environment = development
codeBaseUri = file:$rootDir/sample-app/build/libs/sample-app.jar
packagePrefix = se.crisp
warehouseUri = http://localhost:8180
sensorDumpIntervalSeconds = 10
warehouseUploadIntervalSeconds = 5
"""
    }
}

processResources.dependsOn generateTestDuckProperties
