apply plugin: 'java'
apply plugin: 'com.ofg.uptodate'

uptodate {
    connectionTimeout 20000
    addExcludedVersionPatterns '.*-alpha.*'
    addExcludedVersionPatterns '.*-rc.*'
    addExcludedVersionPatterns '.*-b[0-9]+$'
    addExcludedVersionPatterns '.*-[0-9]$'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": archivesBaseName,
                "Implementation-Version": codekvastDisplayVersion
        )
    }
}

configurations.forEach { it ->
    def config = it
    def configName = config.name
    def taskName = "sync${configName.capitalize()}Dependencies"

    task "$taskName"(type: Sync) {
        group = "Support"
        description = "Syncs $configName dependencies into build/deps/$configName"

        from config
        into "$buildDir/deps/$configName"
    }
}

configurations.all {
    resolutionStrategy {

        eachDependency { details ->
            if (details.requested.name == 'groovy-all') {
                details.useTarget group: details.requested.group, name: 'groovy', version: details.requested.version
            }
        }
    }
}

