apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.7
version = duckVersion

mainClassName = "duck.spike.agent.Agent"

dependencies {
    compile project(":agent:util")
    compile "org.reflections:reflections:0.9.9-RC1"
    compile "org.slf4j:slf4j-api:1.7.7"
    compile "ch.qos.logback:logback-classic:1.1.2"

    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-core:1.9.5"
}

jar {
    exclude "lombok"
}

// We sensor the Sample App by default...
run {
    dependsOn = [":sample:standalone-app:installApp", ":sample:standalone-app:generateDuckConfig"]
    args tasks.getByPath(":sample:standalone-app:generateDuckConfig").configFile
}

applicationDistribution.from("src/main/resources") {
    into "conf"
    include "logback.xml"
    rename { "${it}.sample" }
}

startScripts {
    doLast {
        outputs.files.each { script ->
            logger.info "Adjusting CLASSPATH in {} to start with \$APP_HOME/conf/ ...", script.name
            def text = script.text
            text = text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
            script.text = text
        }
    }
}
