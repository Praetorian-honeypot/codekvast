apply from: "$rootDir/gradle/java-8.gradle"
apply from: "$rootDir/gradle/license.gradle"
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply from: "$rootDir/gradle/standalone-h2server.gradle"
apply plugin: 'org.unbroken-dome.test-sets'

description = "The Codekvast daemon"
applicationName = "codekvast-daemon"
archivesBaseName = "codekvast-daemon"
version = codekvastVersion
mainClassName = "se.crisp.codekvast.agent.daemon.CodekvastDaemon"

import org.apache.tools.ant.filters.ReplaceTokens

testSets {
    integrationTest
}
integrationTest.dependsOn test
check.dependsOn integrationTest

dependencies {
    compileOnly lombok
    compile project(':product:agent:agent-lib')
    compile 'javax.inject:javax.inject:1'
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile 'org.reflections:reflections:0.9.11'
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.flywaydb:flyway-core'
    compile opencsv
    compile 'com.hierynomus:sshj:0.21.0'

    runtime aspectjWeaver
    runtime h2database
    runtime 'ch.qos.logback:logback-classic'

    testCompile lombok
    testCompile junit
    testCompile mockito
    testCompile "org.springframework.boot:spring-boot-starter-test"

    integrationTestCompile project(":product:testsupport")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/application.properties'
        include '**/setenv.bat'
        filter(ReplaceTokens, tokens: gitVersionTokens + ['gradle.name'          : archivesBaseName,
                                                          'gradle.description'   : project.description,
                                                          'gradle.version'       : project.version,
                                                          'gradle.displayVersion': codekvastDisplayVersion,
                                                          'aspectjVersion'       : aspectjVersion]
        )
    }
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
springBoot {
    executable = true
    embeddedLaunchScriptProperties = [
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Daemon',
            'initInfoDescription'     : 'Codekvast Daemon consumes data from the collected apps',
    ]
    excludeDevtools = true
}

bootRepackage {
    // Set a classifier, or else gradle distZip will overwrite the repackaged jar...
    classifier = 'all'
}

flyway {
    url = "jdbc:h2:tcp://localhost/$buildDir/database/codekvast-daemon"
    user = 'codekvast'
    password = 'codekvast'
    locations = ['classpath:/database/migration', 'classpath:/se/crisp/codekvast/agent/daemon/database/migration']
}

run {
    jvmArgs = ['-ea']

    args = [
            "--logging.file=$buildDir/log/codekvast-daemon.log",
            '--logging.level.se.crisp.codekvast=DEBUG',
            '--h2database.options=TRACE_LEVEL_FILE=4;AUTO_SERVER=FALSE;DB_CLOSE_ON_EXIT=FALSE',
            '--codekvast.dataProcessingIntervalSeconds=10',
            "--codekvast.databasePath=tcp://localhost/$buildDir/database",
            '--codekvast.environment=Gradle',

            // '--codekvast.verifyUploadToHostKey=true',
            // '--codekvast.uploadToHost=fjottas.local',
    ]
}
run.dependsOn startDatabase

bootRun {
    jvmArgs = run.jvmArgs
    args = run.args
    addResources = true
}

bootRun.dependsOn startDatabase
