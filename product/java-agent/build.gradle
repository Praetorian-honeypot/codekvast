import java.nio.charset.Charset
import java.util.zip.ZipEntry
import java.util.zip.ZipInputStream

apply from: "$rootDir/gradle/java-7.gradle"
apply from: "$rootDir/gradle/license.gradle"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.unbroken-dome.test-sets'

description = "The javaagent part of Codekvast, the code that runs inside the instrumented app's JVM"
archivesBaseName = "codekvast-javaagent"
version = codekvastVersion

configurations {
    jacocoagent
    sampleappRuntime
}

testSets {
    integrationTest
}
check.dependsOn integrationTest

dependencies {
    compile project(":product:agent-model")
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    compile 'com.google.guava:guava:23.2-jre'
    compile 'com.google.code.gson:gson:2.8.2'
    compile aspectjWeaver

    compileOnly lombok

    testCompile lombok
    testCompile testFrameworks

    integrationTestCompile project(":product:testsupport")
    integrationTestCompile "com.github.tomakehurst:wiremock:2.9.0"
    integrationTestRuntime logback

    jacocoagent 'org.jacoco:org.jacoco.agent:0.7.9:runtime@jar'
}

jar {
    classifier = 'naked'
}

shadowJar {
    classifier = ''

    inputs.file file('build.gradle')
    inputs.files jar.outputs.files
    outputs.file file("build/libs/${archivesBaseName}-${codekvastVersion}.jar")

    manifest {
        attributes(
            "Can-Redefine-Classes": true,
            "Created-By": "Hallin Information Technology AB",
            "Implementation-Title": archivesBaseName,
            "Implementation-Version": codekvastDisplayVersion,
            "Premain-Class": "io.codekvast.javaagent.CodekvastAgent",
            "Specification-Version": codekvastVersion,
            "Boot-Class-Path": archivesBaseName + "-" + codekvastVersion + ".jar"
        )
    }

    dependencies {
        exclude(dependency('com.google.code.findbugs:jsr305:'))
        exclude(dependency('com.google.errorprone:error_prone_annotations:'))
        exclude(dependency('com.google.j2objc:j2objc-annotations:'))
        exclude(dependency('org.codehaus.mojo:animal-sniffer-annotations:'))
    }

    exclude 'META-INF/maven/**'
    exclude 'aspectj*.dtd'
    exclude 'publicsuffixes.gz'

    relocate 'aj.org', 'ck.aj.org'
    relocate 'com', 'ck.com'
    relocate 'javax.annotation', 'ck.javax.annotation'
    relocate 'javax.validation', 'ck.javax.validation'
    relocate 'okhttp3', 'ck.okhttp3'
    relocate 'okio', 'ck.okio'

    relocate('org.aspectj', 'ck.org.aspectj') {
        exclude 'org/aspectj/lang/**' // Cannot be relocated, since that would break weaving
    }

}

task verifyShadowJar {
    group "Verification"
    description "Verifies that the shadow collector jar is correctly relocated to avoid class conflicts"

    def timestampFile = file("$buildDir/verifyShadowJar.timestamp")
    inputs.files shadowJar.outputs.files
    outputs.file timestampFile

    doLast {
        def shadowJarFile = file(shadowJar.outputs.files.asPath)
        logger.info("Verifying relocations in {}", shadowJarFile)

        ZipInputStream zipInputStream = new ZipInputStream(new BufferedInputStream(new FileInputStream(shadowJarFile)))
        InputStreamReader reader = new InputStreamReader(zipInputStream, Charset.forName("UTF-8"))

        ZipEntry zipEntry
        while ((zipEntry = zipInputStream.getNextEntry()) != null) {
            def name = zipEntry.getName()

            assert !name.endsWith(".class") ||
                name.startsWith("ck/") ||
                name.startsWith("io/codekvast/") ||
                name.startsWith("org/aspectj/lang")
        }

        assert shadowJarFile.length() < 6_000_000 // bytes, it is currently approx. 5.3 MB
        timestampFile.parentFile.mkdirs()
        timestampFile.text = "${java.time.Instant.now()}\n"
    }
}
check.dependsOn verifyShadowJar

test {
    systemProperty 'java.util.logging.config.file', 'src/test/resources/logging.properties'
}

integrationTest {
    dependsOn verifyShadowJar
    shouldRunAfter test

    inputs.file file("build.gradle")
    inputs.files shadowJar.outputs.files
    inputs.files sourceSets.integrationTest.output
    outputs.files "build/test-results/integrationTest"

    systemProperty 'integrationTest.codekvastAgent', shadowJar.outputs.files.asPath
    systemProperty 'integrationTest.jacocoAgent', "${configurations.jacocoagent.asPath}=destfile=build/jacoco/integrationTest2.exec,output=file,dumponexit=true"
    systemProperty 'integrationTest.classpath', "build/classes/java/integrationTest/:$configurations.sampleappRuntime.asPath"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task javaagents(type: Sync) {
    from tasks.shadowJar
    into "$buildDir/javaagents"
}

apply plugin: 'maven-publish'
publishing {
    publications {
        shadowJar(MavenPublication) {
            groupId 'io.codekvast'
            artifactId 'codekvast-agent'
            version codekvastVersion
            pom {
                packaging 'jar'
            }

            from components.shadow
            artifact sourcesJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
        }
    }
}

bintray {
    // These are supposed to be defined in $HOME/.gradle/gradle.properties
    user = project.findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintrayKey') ?: System.getenv('BINTRAY_KEY')

    publications = ['shadowJar']

    dryRun = false
    publish = true

    pkg {
        userOrg = 'crisp'
        repo = 'maven-repo'
        name = 'codekvast-agent'
        desc = 'The javaagent part of Codekvast.'
        websiteUrl = 'http://www.codekvast.io'
        vcsUrl = 'https://github.com/crispab/codekvast.git'
        issueTrackerUrl = 'https://github.com/crispab/codekvast/issues'
        licenses = ['MIT']
        githubRepo = 'crispab/codekvast'
        githubReleaseNotesFile = 'RELEASE-NOTES.md'
        publicDownloadNumbers = true

        version {
            name = project.codekvastVersion
            vcsTag = project.gitIdFull
            released = project.gitTimeIso
        }

    }
}

bintrayUpload.dependsOn build, "generatePomFileForShadowJarPublication"
