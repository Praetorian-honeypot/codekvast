apply from: "$rootDir/gradle/java-8.gradle"
apply from: "$rootDir/gradle/license.gradle"
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'flyway'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'spring-boot'

apply from: "$projectDir/gradle/build-devtools.gradle"
apply from: "$projectDir/gradle/build-docker.gradle"
apply from: "$projectDir/gradle/build-frontend.gradle"
apply from: "$projectDir/gradle/build-integrationTest.gradle"
apply from: "$projectDir/gradle/build-functionTest.gradle"

description = 'The central Codekvast warehouse'
applicationName = 'codekvast-warehouse'
archivesBaseName = 'codekvast-warehouse'
version = project.parent.parent.version
group = "crisp"
mainClassName = 'se.crisp.codekvast.warehouse.CodekvastWarehouse'

dependencies {
    compileOnly lombok
    compile project(':product:agent:agent-lib')
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.opencsv:opencsv:$opencsvVersion"
    compile 'javax.inject:javax.inject:1'
    compile 'org.flywaydb:flyway-core'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-logging'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"

    runtime 'org.springframework.boot:spring-boot-starter-actuator'
    runtime "io.springfox:springfox-swagger-ui:${springfoxVersion}"

    runtime mariadbDriver

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

processResources {
    from "src/main/resources", {
        include "*application*.properties"
        expand(project.properties)
    }
    from "src/main/resources", {
        exclude "*application*.properties"
    }
}

//--- Packaging ------------------------------------------------------------------------------------------------------------------
springBoot {
    executable = true
    embeddedLaunchScriptProperties = [
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Warehouse',
            'initInfoDescription'     : 'Codekvast Warehouse aggregates data from many Codekvast Daemons',
    ]
    excludeDevtools = true
}

bootRepackage {
    // Set a classifier, or else gradle distZip will overwrite the repackaged jar...
    classifier = 'all'
}
