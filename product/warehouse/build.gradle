import org.apache.tools.ant.filters.ReplaceTokens

apply from: "$rootDir/gradle/java-8.gradle"
apply from: "$rootDir/gradle/sources-javadoc.gradle"
apply from: "$rootDir/gradle/license.gradle"
apply plugin: 'flyway'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'

description = 'The central Codekvast warehouse'
applicationName = 'codekvast-warehouse'
archivesBaseName = 'codekvast-warehouse'
version = project.parent.version
group = "crisp"
mainClassName = 'se.crisp.codekvast.warehouse.CodekvastWarehouseApplication'

configurations {
    springLoaded
}

dependencies {
    compile lombok
    compile project(':product:agent:agent-lib')
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.opencsv:opencsv:$opencsvVersion"
    compile 'javax.inject:javax.inject:1'
    compile 'org.flywaydb:flyway-core'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-logging'

    runtime 'ch.qos.logback:logback-classic'
    runtime 'org.mariadb.jdbc:mariadb-java-client:1.3.3'

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    springLoaded springLoadedAgent
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/application.properties'
        filter(ReplaceTokens, tokens: gitVersionTokens + ['gradle.name'          : archivesBaseName,
                                                          'gradle.description'   : project.description,
                                                          'gradle.version'       : project.version,
                                                          'gradle.displayVersion': codekvastDisplayVersion]
        )
    }
}

springBoot {
    executable = true
    embeddedLaunchScriptProperties = [
            'initInfoProvides'        : applicationName,
            'initInfoShortDescription': 'Codekvast Warehouse',
            'initInfoDescription'     : 'Codekvast Warehouse aggregates data from many Codekvast Daemons',
    ]
}

bootRepackage {
    // Set a classifier, or else gradle distZip will overwrite the fat jar...
    classifier = 'all'
}

applicationDistribution.from('src/main/resources') {
    into 'config'
    include 'default.properties'
    rename { "${it}.sample.${version}" }
}

applicationDistribution.from(projectDir) {
    include 'docker-compose.yml'
    rename { "docker-compose-${version}.yml" }
}

applicationDefaultJvmArgs = ["-Dlogging.file=/var/log/${applicationName}.log"]

startScripts {
    doLast {
        logger.info 'Adjusting start scripts...'
        windowsScript.text = windowsScript.text
                .replace('CLASSPATH=%APP_HOME%\\lib', 'CLASSPATH=%APP_HOME%\\config\\;%APP_HOME%\\lib')
        unixScript.text = unixScript.text
                .replace('APP_HOME=', 'export APP_HOME=')
                .replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/config/:$APP_HOME/lib')
    }
}

flyway {
    url = 'jdbc:mysql://localhost/codekvast_warehouse'
    user = 'codekvast'
    password = 'codekvast'
    locations = [
            'classpath:/database/migration',
            'classpath:/se/crisp/codekvast/warehouse/migration',
    ]
}

run {
    jvmArgs = ['-ea']

    args = [
            "--logging.file=$buildDir/log/${applicationName}.log",
            '--codekvast.importPath=/tmp/codekvast/.export', // same as daemon export to when started from Gradle
            '--codekvast.importPathPollIntervalSeconds=10',

            // During test
            // '--codekvast.importPath=/home/qolha/Dropbox/Kund/Transmode/codekvast',
            // '--codekvast.deleteImportedFiles=false',
    ]
}

bootRun {
    jvmArgs = [
            "-javaagent:${configurations.springLoaded.asPath}",
            '-noverify',
            '-Dspring.messages.cacheSeconds=1'
    ] + run.jvmArgs

    args = run.args
}

jacoco {
    applyTo run
}

task coverageReport(type: JacocoReport) {
    group 'Documentation'
    description 'Generates a test coverage report'
    sourceSets sourceSets.main

    // One could do
    // executionData test, run
    // but then *all* of build/jacoco/test.exec and run.exec  must exist, or else the report will be skipped.
    // Instead, build a report on what execution data we happen to have.

    enabled = false
    def jacocoDir = file('build/jacoco')
    if (jacocoDir.exists()) {
        jacocoDir.eachFileMatch(~/.*\.exec/) { executionData it; enabled = true }
    }

    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}
build.dependsOn coverageReport

task tagDockerImageWithLatest(type: Exec) {
    commandLine "$projectDir/tagDockerImageWithLatest.sh"
}

distDocker {
    baseImage 'java:8-jre'
    maintainer 'Olle Hallin "olle.hallin@crisp.se"'
    runCommand 'mkdir -p /var/log /tmp/codekvast'
    volume '/var/log'
    volume '/tmp/codekvast'
    tagVersion version
}
distDocker.finalizedBy tagDockerImageWithLatest
