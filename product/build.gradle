buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.ajoberstar:grgit:0.3.1"
    }
}
plugins {
    id 'distribution'
    id 'com.jfrog.bintray' version '0.6'
    id 'com.bmuschko.vagrant' version '2.0'
    id 'org.hidetake.ssh' version '0.4.5'
}


import org.ajoberstar.grgit.Grgit

import java.text.SimpleDateFormat

version = "0.6"

ext.gitCommit = Grgit.open("$rootDir").log(maxCommits: 1)[0]
ext.gitVersionTokens = [
        'git.id'          : "$gitCommit.id".toString(),
        'git.committer'   : "$gitCommit.committer.name <$gitCommit.committer.email>".toString(),
        'git.shortMessage': "$gitCommit.shortMessage".toString(),
        'git.time'        : "${new SimpleDateFormat('yyyy-MM-dd HH:mm:ss').format(new Date(gitCommit.time * 1000L))}".toString(),
]

distributions {
    agent {
        baseName = 'codekvast-agent'
        contents {
            from tasks.getByPath(":product:agent:codekvast-agent:installApp")
            from tasks.getByPath(":product:agent:collector:shadowJar"), { into "endorsed" }
            from tasks.getByPath(":product:agent:codekvast-agent:javadocJar"), { into "src" }
            from tasks.getByPath(":product:agent:codekvast-agent:sourcesJar"), { into "src" }
            from tasks.getByPath(":product:agent:collector:javadocJar"), { into "src" }
            from tasks.getByPath(":product:agent:collector:sourcesJar"), { into "src" }
            from tasks.getByPath(":product:agent:util:javadocJar"), { into "src" }
            from tasks.getByPath(":product:agent:util:sourcesJar"), { into "src" }
        }
    }

    server {
        baseName = 'codekvast-server'
        contents {
            from tasks.getByPath(":product:server:codekvast-server:installApp")
        }
    }

    web {
        baseName = 'codekvast-web'
        contents {
            from tasks.getByPath(":product:web:installApp")
        }
    }
}

task build(dependsOn: [agentDistZip, serverDistZip, webDistZip]) {
    description = "Builds distribution zips for agent, server and web"
    group "build"
}

bintray {
    // These are supposed to be defined in $HOME/.gradle/gradle.properties
    user = "$bintrayUser"
    key = "$bintrayKey"

    filesSpec {
        from tasks.getByPath(":product:agentDistZip")
        into "agent"
    }

    dryRun = false
    publish = true

    pkg {
        repo = 'codekvast'
        userOrg = 'crisp'
        name = 'agent'
        desc = 'Codekvast is a Runtime Intelligence tool used for identifying Truly Dead Code, i.e., code that no-one has accessed in a ' +
                'certain period of time'
        websiteUrl = 'http://codekvast.crisp.se'
        licenses = ['MIT']
        labels = ['runtime-intelligence', 'dead-code', 'dead-code-detector', 'truly-dead-code', 'YANIA', 'java', 'java-6', 'jvm']

        publicDownloadNumbers = false
    }
}

ssh.settings {
    knownHosts = allowAnyHosts    // Disable host key verification
    pty = true
}

remotes {
    web {
        role 'all'
        host = 'codekvast.crisp.se'
        user = 'ubuntu'
        identity = new File(System.properties['user.home'] + '/.ssh', 'Codekvast.pem')
    }
}

task prepareTargetServers << {
    ssh.run {
        session(remotes.role('all')) {
            executeSudo "apt-get update"
            executeSudo "apt-get upgrade -y"
            executeSudo "apt-get install -y unzip"
            executeSudo "apt-get install -y openjdk-7-jdk"
        }
    }
}

task deployWeb {
    group "Provisioning"
    description "Deploys Codekvast-web"
    dependsOn webDistZip

    def zip = file("$buildDir/distributions/codekvast-web-${version}.zip")
    def confDir = file('src/provision/web/conf/')
    def deployStatusFile = file("$buildDir/tmp/deploy-web.status")

    inputs.file zip
    inputs.dir file('src/provision/web')
    outputs.file deployStatusFile

    doLast {

        println "Deploying $zip ..."

        ssh.run {
            session(remotes.web) {
                put zip, "."
                execute "unzip -o ${zip.name}"

                put confDir, "codekvast-web-${version}"
            }
        }
        deployStatusFile.parentFile.mkdirs()
        deployStatusFile.text = "$zip deployed at ${new Date()}\n"
    }
}

task getPeople << {
    ssh.run {
        session(remotes.web) {
            get 'people.csv', buildDir
        }
    }
}
